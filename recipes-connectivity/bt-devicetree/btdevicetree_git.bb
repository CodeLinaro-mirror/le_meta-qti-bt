DESCRIPTION = "QTI BT devicetree"
LICENSE = "BSD-3-Clause"
LIC_FILES_CHKSUM = "file://${COREBASE}/meta/files/common-licenses/\
${LICENSE};md5=550794465ba0ec5312d6919e203a55f9"

inherit linux-kernel-base deploy
FILESEXTRAPATHS:prepend := "${WORKSPACE}:"
SRC_URI     =  "file://bluetooth/bt-devicetree/"
S = "${WORKDIR}/bluetooth/bt-devicetree"
DEPENDS += "virtual/kernel coreutils-native rsync-native"

KERNEL_VERSION = "${@get_kernelversion_file("${STAGING_KERNEL_BUILDDIR}")}"
EXT_MODULES = "${@os.path.relpath("${S}", "${KERNEL_PLATFORM_PATH}")}"

do_configure[noexec] = "1"

do_compile[depends] += "virtual/kernel:do_shared_workdir"
do_compile[cleandirs] += "${WORKDIR}/out/${KERNEL_DEFCONFIG}"
do_compile[lockfiles] = "${@ '${TMPDIR}/build_modules.lock' if d.getVar('MSM_KERNEL_VERSION') not in ['6.1', '6.6'] else ''}"

do_compile() {
      cd ${KERNEL_PLATFORM_PATH}
      BUILD_CONFIG=${KERNEL_BUILD_CONFIG} \
      EXT_MODULES=${EXT_MODULES} \
      KERNEL_KIT=${KERNEL_PREBUILT_PATH} \
      MODULE_OUT=${S} \
      OUT_DIR=${WORKDIR}/out/${KERNEL_DEFCONFIG} \
      INPLACE_COMPILE=y \
      ./build/build_module.sh dtbs
}

do_deploy() {
    install -d ${DEPLOYDIR}/tech_dtbs
    install -m 0644 ${S}/*.dtbo ${DEPLOYDIR}/tech_dtbs/
}

addtask do_deploy after do_install
ALLOW_EMPTY:${PN} = "1"
